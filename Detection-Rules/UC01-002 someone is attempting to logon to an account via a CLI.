// someone is attempting to logon to an account via a CLI.
// Title: UC01-002 someone is attempting to logon to an account via a CLI. 
// ID: $add here the Sentinel Rule ID
// Description: This detection rule triggers when someone is attempting to access an account via Microsoft PowerShell, Microsoft Graph, or Azure CLI. It is known that bad actors are using these tools to get access and perform malicious activities. 'Normal' users are unlikely to use these tooling compared to IT administrators. For these persons a watchlist can be used in order to exclude their activities from certain IP addresses. 
// Severity: Medium
// Tactics: Initial Access, Discovery
// Techniques: T1078 - Valid Accounts, T1082 - System Information Discovery, T1580 - Cloud Service Discovery
// Creation date: 29 November 2024
// Created by: @82BBB00 - https://github.com/82BBB00
// Last modified date: XXX
// Last modified by: XXX
// QueryFrequency: : PT5M
// QueryPeriod: PT10M
// The actual KQL query will start below

let Trustedlist = _GetWatchlist('$add here the name of the watchlist');
SigninLogs
// These applications are used for the commands 'az login', 'Connect-AzAccount', and 'Connect-MgGraph'
| where AppDisplayName in ("Microsoft Azure CLI", "Microsoft Azure PowerShell", "Microsoft Graph Command Line Tools")
// Excluding Intune enrolled devices
| where DeviceDetail.isManaged <> true
// Searching only on UPN and not IDs
| where UserPrincipalName has "@"
| project TimeGenerated, UserPrincipalName, AppDisplayName, ClientAppUsed, DeviceDetail, UserAgent, ResultType, IPAddress
// Joining username and IP from watchlist on signinlogs (Allfields used due to some errors projecting the correct entities)
| join kind=leftanti (
    Trustedlist
    | extend AllFields = pack_all()
| extend UserPrincipalName = tostring(AllFields.UserPrincipalName)
| extend IPAddress = tostring(AllFields.[" IPAddress"])
| project UserPrincipalName, IPAddress)
on UserPrincipalName, IPAddress
